[Centro Comercial]
			;CENTRO EN COMERCIAL
POISSON   		FUNCTION   RN8,C24   ;exponencial negativa con valor medio igual a 1
0,0/.1,.104/.2,.222/.3,.355/.4,.509/.5,.69/.6,.915/.7,1.2/.75,1.38/.8,1.6/
.84,1.83/.88,2.12/.9,2.3/.92,2.52/.94,2.81/.95,2.99/.96,3.2/.97,3.5/.98,3.9/
.99,4.6/.995,5.3/.998,6.2/.999,7/.9998,8
TIPO 			FUNCTION RN2,D4
.2,1/.55,2/.70,3/1,4
BIFBAR 		FUNCTION P$TIPO,D4
1,50/2,350/3,800/4,400
MEDIA_CAJA 	FUNCTION P$TIPO,D4
1,40/2,60/3,120/4,90
DESVIO_CAJA 	FUNCTION P$TIPO,D4
1,20/2,40/3,60/4,60
DESVIO_CAJA 	VARIABLE FN$DESVIO_CAJA 
MAX_COLA 		FUNCTION P$TIPO,D4
1,2/2,4/3,8/4,3
GENTECAJAS		VARIABLE Q1+Q2+Q3+Q4+Q5
MESAS 		STORAGE 1000
GONDOLAS 		STORAGE 10000
PROMOCION 		STORAGE 1
TTOTAL		TABLE M1, 500,200,20					; TABLA TIEMPO TOTAL EN EL SISTEMA
CANTPERSONAS	TABLE S$MESAS, 0,1,15				; TABLA CANTIDAD DE PERSONAS COMIENDO EN EL BAR
CANTPERSCJAS 	TABLE V$GENTECAJAS, 0,2,10				; TABLA CANTIDAD DE PERSONAS EN CAJAS
COMPRANDO		TABLE S$GONDOLAS, 5,2,10				; TABLA CANTIDAD DE PERSONAS COMPRANDO EN GONDOLAS
TPROMO		QTABLE PROMOCION,0,5,20
			INITIAL X$MED,90					; SUBSISTEMA DE CONTROL DE CAJAS
			INITIAL X$CAJAS,3
			GENERATE ,,7200,1
			SAVEVALUE MED,50
			SAVEVALUE CAJAS,5
			ADVANCE 7200
			SAVEVALUE MED,70
			SAVEVALUE CAJAS,4
			TERMINATE
			GENERATE X$MED,FN$POISSON				; INGRESAN CLIENTE CON MEDIA Y DISTRIBUCION POISSON
			ASSIGN TIPO,FN$TIPO					; ASIGNAMOS EL TIPO
			ADVANCE 45,15						; CRUZAN EL HALL
			TRANSFER FN$BIFBAR,,BAR				; BIFURCAN AL BAR EN FUNCION DEL CLIENTE
			TRANSFER ,R_REUNION
BAR 			ADVANCE 90,30						; EN EL BAR DEMORAN EN RECOGER LA COMIDA
			QUEUE COMEDOR
			SEIZE COMEDOR      
			DEPART COMEDOR
			ADVANCE 50,25						; LE COBRAN LA COMIDA
			RELEASE COMEDOR
			QUEUE MESAS      
			ENTER MESAS
			DEPART MESAS
			ADVANCE 900,450						; SE QUEDAN COMIENDO EN EL COMEDOR
			LEAVE MESAS
R_REUNION 		ADVANCE 45,15						; SE DIRIGEN AL PUESTO DE PROMOCION
			TEST LE Q$PROMOCION,FN$MAX_COLA,CONT_SUPER	; IMPACIENCIA DE GRADO 1. 
			QUEUE PROMOCION
			ENTER PROMOCION
			DEPART PROMOCION
			ADVANCE 60,15
			LEAVE	PROMOCION
CONT_SUPER		QUEUE	GONDOLAS
			ENTER	GONDOLAS
			DEPART GONDOLAS
			ADVANCE 900,300						; COMPRAN ARTICULOS EN GONDOLAS AUTOSERVICIO
			LEAVE	GONDOLAS
			SELECT NU CAJA,1,X$CAJAS,,,OCUP
			TRANSFER ,PAGAR
OCUP    		SELECT MIN CAJA,1,X$CAJAS,,Q
PAGAR			QUEUE P$CAJA
			SEIZE P$CAJA
			DEPART P$CAJA
			ADVANCE FN$MEDIA_CAJA,V$DESVIO_CAJA		; PAGAR EN FUNCION DEL CLIENTE
			RELEASE P$CAJA
	 		ADVANCE 45,15
			TABULATE TTOTAL
			TERMINATE
          		GENERATE 120						; CADA DOS MINUTOS
			TABULATE CANTPERSONAS					; TABULAR CANTIDAD DE PERSONAS COMIENDO
			TABULATE CANTPERSCJAS 				; TABULAR CANTIDAD DE PERSONAS TOTAL EN FRENTE DE LAS CAJAS
			TERMINATE
      		GENERATE 60,,3600					; CADA MINUTO EMPEZANDO A LAS 10HS
			TABULATE COMPRANDO					; TABULAR CANTIDAD DE PERSONAS COMPRANDO EN GONDOLAS
			TERMINATE
			GENERATE 3600
			TERMINATE 1
			;START 6
      


<\F>
Nombre=
Descripcion=
Version=
Autor=
Empresa=
Opciones=
